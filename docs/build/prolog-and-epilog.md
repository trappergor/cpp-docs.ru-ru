---
title: Пролог и эпилог для 64-разрядных систем
ms.date: 12/17/2018
ms.assetid: 0453ed1a-3ff1-4bee-9cc2-d6d3d6384984
ms.openlocfilehash: d0b7444af6e434a09f6af5f5b1c144b46c79ad56
ms.sourcegitcommit: c123cc76bb2b6c5cde6f4c425ece420ac733bf70
ms.translationtype: HT
ms.contentlocale: ru-RU
ms.lasthandoff: 04/14/2020
ms.locfileid: "81328436"
---
# <a name="x64-prolog-and-epilog"></a>Пролог и эпилог для 64-разрядных систем

Каждая функция, которая выделяет пространство стека, вызывает другие функции, сохраняет энергонезависимые регистры или использует обработку исключений, должна содержать пролог с ограничениями адресов, описанными в данных очистки, связанных с соответствующей записью таблицы функций. Дополнительные сведения см. в разделе [Обработка исключений x64](../build/exception-handling-x64.md). Пролог сохраняет регистры аргументов в домашних адресах при необходимости, отправляет энергонезависимые регистры в стек, выделяет фиксированную часть стека для локальных и временных переменных и может устанавливать указатель кадра. Связанные данные очистки должны описывать действие пролога и предоставлять сведения, необходимые для отмены воздействия кода пролога.

Если фиксированное выделение в стеке составляет более одной страницы (то есть более 4096 байт), то возможно, что выделение стека может охватывать несколько страниц виртуальной памяти и, следовательно, выделение должно быть предварительно проверено. Для этой цели предоставляется специальная подпрограмма, вызываемая из пролога, которая не уничтожает никакие регистры аргументов.

Предпочтительным способом сохранения энергонезависимых регистров является перемещение их в стек перед фиксированным выделением стека. Если фиксированное выделение стека выполняется до сохранения энергонезависимых регистров, скорее всего, для адресации сохраненной области регистров требуется 32-разрядное смещение. (Считается, что принудительная отправка регистров выполняется так же быстро, как перемещения, и эта особенность сохранится в обозреваемом будущем, несмотря на неявную зависимость между операциями отправки.) Энергонезависимые регистры можно сохранять в любом порядке. Однако первое использование энергонезависимого регистра в прологе должно быть сохранено.

## <a name="prolog-code"></a>Код пролога

Код для типичного пролога может выглядеть следующим образом:

```MASM
    mov    [RSP + 8], RCX
    push   R15
    push   R14
    push   R13
    sub    RSP, fixed-allocation-size
    lea    R13, 128[RSP]
    ...
```

Этот пролог хранит регистр аргумента RCX в домашнем расположении, сохраняет энергонезависимые регистры R13-R15, выделяет фиксированную часть кадра стека и устанавливает указатель кадра, который указывает на 128 байтов в фиксированной области выделения. Использование смещения позволяет обращаться к большей части фиксированной области выделения с однобайтовыми смещениями.

Если фиксированный размер выделения не больше одной страницы памяти, то перед изменением RSP необходимо вызвать вспомогательную функцию. Эта вспомогательная функция, `__chkstk`, выполняет проверку ожидаемого диапазона стека, чтобы обеспечить правильное расширение стека. В этом случае предыдущий пример пролога будет выглядеть следующим образом:

```MASM
    mov    [RSP + 8], RCX
    push   R15
    push   R14
    push   R13
    mov    RAX,  fixed-allocation-size
    call   __chkstk
    sub    RSP, RAX
    lea    R13, 128[RSP]
    ...
```

Вспомогательная функция `__chkstk` не будет изменять регистры, кроме R10, R11 и кодов условий. В частности, она возвратит RAX без изменений и оставит все энергонезависимые регистры и регистры, передающие аргументы, без изменений.

## <a name="epilog-code"></a>Код эпилога

Код эпилога существует при каждом выходе из функции. Обычно существует только один пролог, но эпилогов может быть несколько. Код эпилога усекает стек до фиксированного размера выделения (при необходимости), освобождает фиксированное выделение стека, восстанавливает энергонезависимые регистры, извлекая сохраненные значения из стека, и возвращает результат.

Код эпилога должен соответствовать определенному набору правил для кода очистки для надежной очистки с помощью исключений и прерываний. Эти правила уменьшают объем необходимых данных для очистки, так как для описания каждого эпилога не требуется дополнительных данных. Вместо этого код очистки может определить, что эпилог выполняется путем сканирования с продвижением вперед через поток кода для определения эпилога.

Если в функции не используется указатель кадра, то эпилог должен сначала отменить выделение фиксированной части стека, и затем извлекаются энергонезависимые регистры, а управление возвращается вызывающей функции. Например, примененная к объекту директива

```MASM
    add      RSP, fixed-allocation-size
    pop      R13
    pop      R14
    pop      R15
    ret
```

Если в функции используется указатель кадра, то стек необходимо обрезать до фиксированного выделения до выполнения эпилога. Это действие технически не является частью эпилога. Например, следующий эпилог можно использовать для отмены ранее использованного пролога:

```MASM
    lea      RSP, -128[R13]
    ; epilogue proper starts here
    add      RSP, fixed-allocation-size
    pop      R13
    pop      R14
    pop      R15
    ret
```

На практике при использовании указателя кадра нет веских причин для настройки RSP в два этапа, поэтому вместо этого будет использоваться следующий эпилог:

```MASM
    lea      RSP, fixed-allocation-size - 128[R13]
    pop      R13
    pop      R14
    pop      R15
    ret
```

Эти формы являются единственными правильными для эпилога. Он должен состоять из `add RSP,constant` или `lea RSP,constant[FPReg]`, за которым следует последовательность из нуля или более 8-байтовых извлечений регистра, а также `return` или `jmp`. (В эпилоге допускается только подмножество операторов `jmp`. Подмножество является исключительно классом операторов `jmp` со ссылками на память ModRM, где значение поля mod ModRM равно 00. Запрещено использовать в эпилоге инструкции `jmp` со значением поля mod ModRM 01 или 10. См. таблицу A-15 в руководстве программиста по архитектуре AMD x86-64, том 3: инструкции общего назначения и системные инструкции. В этом руководстве содержатся дополнительные сведения о допустимых ссылках ModRM.) Никакой другой код не может использоваться. В частности, в эпилоге ничего нельзя запланировать, включая загрузку возвращаемого значения.

Если указатель кадра не используется, эпилог должен использовать `add RSP,constant` для освобождения фиксированной части стека. Использовать `lea RSP,constant[RSP]` вместо этого нельзя. Это ограничение существует для того, чтобы в коде очистки было меньше шаблонов для распознавания при поиске эпилогов.

Следуя этим правилам, код очистки позволяет определить, что в настоящее время эпилог выполняется, и имитировать выполнение оставшейся части эпилога, чтобы разрешить повторное создание контекста вызывающей функции.

## <a name="see-also"></a>См. также

[Программные соглашения для X64](x64-software-conventions.md)
